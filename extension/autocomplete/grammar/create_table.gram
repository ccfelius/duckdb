CreateStatement <- 'CREATE'i OrReplace? Temporary? (CreateTableStmt / CreateMacroStmt / CreateSequenceStmt / CreateTypeStmt / CreateSchemaStmt / CreateViewStmt / CreateIndexStmt)
OrReplace <- 'OR'i 'REPLACE'i
Temporary <- 'TEMP'i / 'TEMPORARY'i

CreateTableStmt <- 'TABLE'i IfNotExists? QualifiedTableName (CreateColumnList / CreateTableAs)

CreateTableAs <- IdentifierList? 'AS'i SelectOrParens
SelectOrParens <- SelectStatement / Parens(SelectStatement)
IdentifierList <- List(Identifier)
CreateColumnList <- Parens(CreateTableColumnList)
IfNotExists <- 'IF'i 'NOT'i 'EXISTS'i
QualifiedTableName <- CatalogQualification? SchemaQualification? Identifier
CatalogQualification <- CatalogName '.'
SchemaQualification <- SchemaName '.'
TableQualification <- TableName '.'

CreateTableColumnList <- List(CreateTableColumnElement)
CreateTableColumnElement <- ColumnDefinition / TopLevelConstraint
ColumnDefinition <- Identifier TypeOrGenerated ColumnConstraint*
TypeOrGenerated <- Type / GeneratedColumn
ColumnConstraint <- NotNullConstraint / UniqueConstraint / PrimaryKeyConstraint / DefaultValue / CheckConstraint
NotNullConstraint <- 'NOT'i 'NULL'i
UniqueConstraint <- 'UNIQUE'i
PrimaryKeyConstraint <- 'PRIMARY'i 'KEY'i
DefaultValue <- 'DEFAULT'i Expression
CheckConstraint <- 'CHECK'i Parens(Expression)

TopLevelConstraint <- TopPrimaryKeyConstraint / CheckConstraint
TopPrimaryKeyConstraint <- 'PRIMARY'i 'KEY'i ColumnIdList
ColumnIdList <- Parens(List(Identifier))

PlainIdentifier <-  !ReservedKeyword <[a-z_]i[a-z0-9_]i*>
QuotedIdentifier <- '"' [^"]* '"'
Identifier <- QuotedIdentifier / PlainIdentifier

GeneratedColumn <- Type? Generated? 'AS'i Parens(Expression)  GeneratedColumnType?

Generated <- 'GENERATED'i AlwaysOrByDefault?
AlwaysOrByDefault <- 'ALWAYS'i / ('BY'i 'DEFAULT'i)
GeneratedColumnType <- 'VIRTUAL'i / 'STORED'i

SelectStatement <- SimpleSelect (SetopClause SimpleSelect)*

SetopClause <- ('UNION'i / 'EXCEPT'i / 'INTERSECT'i) 'ALL'i?

SimpleSelect <- WithClause? SelectFrom WhereClause? GroupByClause? HavingClause? OrderByClause? LimitClause? OffsetClause?

SelectFrom <- (SelectClause FromClause?) / (FromClause SelectClause?)
WithStatement <- Identifier 'AS'i SubqueryReference
WithClause <- 'WITH'i List(WithStatement)
SelectClause <- 'SELECT'i ('*' / List(AliasedExpression))
ColumnAliases <- Parens(List(Identifier))

InnerTableRef <- (SubqueryReference / BaseTableRef) TableAlias?

TableRef <- InnerTableRef JoinClause?
BaseTableRef <- CatalogQualification? SchemaQualification? TableName

TableAlias <- 'AS'i? Identifier ColumnAliases?

JoinClause <- RegularJoinClause / JoinWithoutOnClause
RegularJoinClause <- JoinType? InnerOuter? 'JOIN'i InnerTableRef 'ON'i Expression
JoinWithoutOnClause <- JoinPrefix 'JOIN'i InnerTableRef

InnerOuter <- 'INNER'i / 'OUTER'i
JoinType <- 'LEFT'i / 'RIGHT'i / 'SEMI'i / 'ANTI'i
JoinPrefix <- 'CROSS'i / ('NATURAL'i JoinType) / 'POSITIONAL'i

FromClause <- 'FROM'i List(TableRef)
WhereClause <- 'WHERE'i Expression
GroupByClause <- 'GROUP'i 'BY'i GroupByExpressions
HavingClause <- 'HAVING'i Expression

GroupByExpressions <- List(Expression) / 'ALL'i

SubqueryReference <- Parens(SelectStatement)

OrderByExpression <- Expression ('DESC'i / 'ASC'i)?  ('NULLS'i 'FIRST'i / 'LAST'i)?
OrderByClause <- 'ORDER'i 'BY'i OrderByExpressions
OrderByExpressions <- List(OrderByExpression) / 'ALL'i

LimitClause <- 'LIMIT'i NumberLiteral
OffsetClause <- 'OFFSET'i NumberLiteral

AliasedExpression <- Expression ('AS'i? Identifier)?

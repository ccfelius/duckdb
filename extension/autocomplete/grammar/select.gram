SelectStatement <- SelectOrParens (SetopClause SelectStatement)*

SetopClause <- ('UNION'i / 'EXCEPT'i / 'INTERSECT'i) DistinctOrAll? ByName?
ByName <- 'BY'i 'NAME'i
SelectOrParens <- BaseSelect / Parens(SelectStatement)

BaseSelect <- (SimpleSelect / ValuesClause) OrderByClause? LimitClause? OffsetClause?

SimpleSelect <- WithClause? SelectFrom WhereClause? GroupByClause? HavingClause? WindowClause? QualifyClause?

SelectFrom <- (SelectClause FromClause?) / (FromClause SelectClause?)
WithStatement <- Identifier InsertColumnList? 'AS'i SubqueryReference
WithClause <- 'WITH'i Recursive? List(WithStatement)
Recursive <- 'RECURSIVE'i
SelectClause <- 'SELECT'i DistinctClause? TargetList
TargetList <- List(AliasedExpression)
ColumnAliases <- Parens(List(Identifier))

DistinctClause <- ('DISTINCT'i DistinctOn?) / 'ALL'i
DistinctOn <- 'ON'i Parens(List(Expression))

InnerTableRef <- ValuesRef / TableFunction / TableSubquery / BaseTableRef / ParensTableRef

TableRef <- InnerTableRef JoinClause*
TableSubquery <- SubqueryReference TableAlias?
BaseTableRef <- BaseTableName TableAlias?
ValuesRef <- ValuesClause TableAlias?
ParensTableRef <- Parens(TableRef)

BaseTableName <- CatalogQualification? SchemaQualification? TableName

TableFunction <- QualifiedTableFunction TableFunctionArguments TableAlias?
QualifiedTableFunction <- CatalogQualification? SchemaQualification? TableFunctionName
TableFunctionArguments <- Parens(List(FunctionArgument)?)
FunctionArgument <- NamedParameter / Expression
NamedParameter <- Identifier NamedParameterAssignment Expression
NamedParameterAssignment <- ':=' / '=>'

TableAlias <- 'AS'i? Identifier ColumnAliases?

JoinClause <- RegularJoinClause / JoinWithoutOnClause
RegularJoinClause <- JoinType? 'JOIN'i InnerTableRef JoinQualifier
JoinWithoutOnClause <- JoinPrefix 'JOIN'i InnerTableRef
JoinQualifier <- OnClause / UsingClause
OnClause <- 'ON'i Expression
UsingClause <- 'USING'i Parens(List(Identifier))

OuterJoinType <- 'FULL'i / 'LEFT'i / 'RIGHT'i
JoinType <- (OuterJoinType 'OUTER'i?) / 'SEMI'i / 'ANTI'i / 'INNER'i
JoinPrefix <- 'CROSS'i / ('NATURAL'i JoinType?) / 'POSITIONAL'i / 'ASOF'i

FromClause <- 'FROM'i List(TableRef)
WhereClause <- 'WHERE'i Expression
GroupByClause <- 'GROUP'i 'BY'i GroupByExpressions
HavingClause <- 'HAVING'i Expression
QualifyClause <- 'QUALIFY'i Expression
WindowClause <- 'WINDOW'i List(WindowDefinition)
WindowDefinition <- Identifier 'AS'i WindowFrameDefinition

GroupByExpressions <- GroupByList / 'ALL'i
GroupByList <- List(GroupByExpression)
GroupByExpression <- EmptyGroupingItem / CubeOrRollupClause / GroupingSetsClause / Expression
EmptyGroupingItem <- '(' ')'
CubeOrRollupClause <- CubeOrRollup Parens(List(Expression))
CubeOrRollup <- 'CUBE'i / 'ROLLUP'i
GroupingSetsClause <- 'GROUPING'i 'SETS'i Parens(GroupByList)

SubqueryReference <- Parens(SelectStatement)

OrderByExpression <- Expression DescOrAsc? NullsFirstOrLast?
DescOrAsc <- 'DESC'i / 'DESCENDING'i / 'ASC'i / 'ASCENDING'i
NullsFirstOrLast <- 'NULLS'i 'FIRST'i / 'LAST'i
OrderByClause <- 'ORDER'i 'BY'i OrderByExpressions
OrderByExpressions <- List(OrderByExpression) / OrderByAll
OrderByAll <- 'ALL'i DescOrAsc? NullsFirstOrLast?

LimitClause <- 'LIMIT'i LimitValue
OffsetClause <- 'OFFSET'i LimitValue
LimitValue <- 'ALL'i / (Expression '%'?) / (NumberLiteral 'PERCENT'i)

AliasedExpression <- Expression ('AS'i? Identifier)?

ValuesClause <- 'VALUES'i List(ValuesExpressions)
ValuesExpressions <- Parens(List(Expression))

ColumnReference <- CatalogQualification? SchemaQualification? TableQualification? ColumnName
FunctionExpression <- FunctionIdentifier Parens(DistinctOrAll? List(FunctionArgument)? OrderByClause?) WithinGroupClause? FilterClause? ExportClause? OverClause?
FunctionIdentifier <- CatalogQualification? SchemaQualification? TableQualification? FunctionName
DistinctOrAll <- 'DISTINCT'i / 'ALL'i
ExportClause <- 'EXPORT_STATE'i
WithinGroupClause <- 'WITHIN'i 'GROUP'i Parens(OrderByClause)
FilterClause <- 'FILTER' Parens('WHERE'i? Expression)

ParenthesisExpression <- Parens(List(Expression))
LiteralExpression <- StringLiteral / NumberLiteral / 'NULL'i / 'TRUE'i / 'FALSE'i
CastExpression <- CastOrTryCast Parens(Expression 'AS'i Type)
CastOrTryCast <- 'CAST'i / 'TRY_CAST'i

ExtractExpression <- 'EXTRACT'i Parens(ColumnReference 'FROM'i Expression)
StarExpression <- '*'i
SubqueryExpression <- 'NOT'i? 'EXISTS'i? SubqueryReference
CaseExpression <- 'CASE'i ColumnReference? CaseWhenThen CaseWhenThen* CaseElse? 'END'i
CaseWhenThen <- 'WHEN'i Expression 'THEN'i Expression
CaseElse <- 'ELSE'i Expression
TypeLiteral <- Identifier StringLiteral
IntervalLiteral <- 'INTERVAL'i IntervalParameter IntervalUnit?
IntervalParameter <- StringLiteral / NumberLiteral / Parens(Expression)
IntervalUnit <- Identifier
SubstringExpression <- 'SUBSTRING'i Parens(Expression 'FROM'i NumberLiteral 'FOR'i NumberLiteral)
FrameClause <- Framing FrameExtent WindowExcludeClause?
Framing <- 'ROWS'i / 'RANGE'i / 'GROUPS'i
FrameExtent <- FrameBound / ('BETWEEN'i FrameBound 'AND'i FrameBound)
FrameBound <- ('UNBOUNDED'i 'PRECEDING'i) / ('UNBOUNDED'i 'FOLLOWING'i) / ('CURRENT'i 'ROW'i) / (Expression 'PRECEDING'i) / (Expression 'FOLLOWING'i)
WindowExcludeClause <- 'EXCLUDE'i WindowExcludeElement
WindowExcludeElement <- ('CURRENT'i 'ROW'i) / 'GROUP'i / 'TIES'i / ('NO'i 'OTHERS'i)
OverClause <- 'OVER'i WindowFrame
WindowFrame <- WindowFrameDefinition / Identifier / Parens(Identifier)
WindowFrameDefinition <- Parens(WindowFrameContents)
WindowFrameContents <- WindowPartition? OrderByClause? FrameClause?
WindowPartition <- 'PARTITION'i 'BY'i List(Expression)
PrefixExpression <- PrefixOperator Expression
PrefixOperator <- '-' / '+'
ListExpression <- 'ARRAY'i? '[' List(Expression)? ']'
StructExpression <- '{' List(StructField) '}'
StructField <- Expression ':'i Expression
MapExpression <- 'MAP'i StructExpression
GroupingExpression <- GroupingOrGroupingId Parens(List(Expression))
GroupingOrGroupingId <- 'GROUPING'i / 'GROUPING_ID'i
Parameter <- '?'

ListComprehensionExpression <- '['i Expression 'FOR'i Expression ListComprehensionFilter? ']'
ListComprehensionFilter <- 'IF'i Expression

SingleExpression <-
	Parameter /
    SubqueryExpression /
    ParenthesisExpression /
    IntervalLiteral /
    TypeLiteral /
    SubstringExpression /
    CaseExpression /
    StarExpression /
    CastExpression /
    ExtractExpression /
    GroupingExpression /
    FunctionExpression /
    ColumnReference /
    LiteralExpression /
    PrefixExpression /
    ListComprehensionExpression /
    ListExpression /
    StructExpression /
    MapExpression

ArithmeticOperator <- '+' / '-' / '*' / '/' / '//' / '%' / '^' / '**' / '||'
LikeOperator <- 'NOT'i? 'LIKE'i
InOperator <- 'NOT'i? 'IN'i
IsOperator <- 'IS'i 'NOT'i? DistinctFrom?
DistinctFrom <- 'DISTINCT'i 'FROM'i
ConjunctionOperator <- 'OR'i / 'AND'i
ComparisonOperator <-  '=' / '<=' / '>=' / '<' / '>' / '<>' / '!=' / '=='
BetweenOperator <- 'BETWEEN'i
CollateOperator <- 'COLLATE'i

Operator <-
    ArithmeticOperator /
    ComparisonOperator /
    ConjunctionOperator /
    LikeOperator /
    InOperator /
    IsOperator /
    BetweenOperator /
    CollateOperator

CastOperator <- '::' Type

BaseExpression <- SingleExpression CastOperator*
Expression <- BaseExpression RecursiveExpression*
RecursiveExpression <- (Operator Expression) / SliceExpression
SliceExpression <- '[' SliceBound ']'
SliceBound <- Expression? (':' Expression?)? (':' Expression?)?
